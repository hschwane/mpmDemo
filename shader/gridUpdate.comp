#version 450 core
#extension GL_ARB_compute_variable_group_size : require

// --------------------------------------
// uniforms
uniform vec2 simDomain;
uniform vec2 externalAcc;
uniform float timestep;

// --------------------------------------
// texture bindings
uniform layout(r32f) restrict image2D gridVX;
uniform layout(r32f) restrict image2D gridVY;
uniform layout(r32f) restrict image2D gridM;
uniform layout(r16f) restrict readonly image2D collision;

// --------------------------------------
// compute parameters
layout(local_size_variable) in;

void main()
{
    ivec2 gridSize = imageSize(gridM);
    ivec2 node;
    for(  node.y= int(gl_GlobalInvocationID.y); node.y < gridSize.y; node.y += int(gl_NumWorkGroups.y * gl_LocalGroupSizeARB.y))
        for(  node.x= int(gl_GlobalInvocationID.x); node.x < gridSize.x; node.x += int(gl_NumWorkGroups.x * gl_LocalGroupSizeARB.x))
        {
            // load momentum and mass
            float mass = imageLoad(gridM,node).x;

            if(mass > 0)
            {
                // calc celocity and add external acc
                vec2 vel;
                vel.x = imageLoad(gridVX,node).x;
                vel.y = imageLoad(gridVY,node).x;
                vel /= mass;
                vel += timestep * externalAcc;

                // slip boundaries
                if (node.x < 3 || node.x > simDomain.x-4) vel.x = 0;
                if (node.y < 3 || node.y > simDomain.y-4) vel.y = 0;

                // TODO: handle collision and proper boundaries

                imageStore(gridVX, node, vec4(vel.x,0,0,0));
                imageStore(gridVY, node, vec4(vel.y,0,0,0));
            }
        }
}
